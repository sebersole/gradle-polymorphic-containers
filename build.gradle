plugins {
    id 'groovy'
    id 'java'
}

group 'com.github.sebersole'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

abstract class ExtensionSpec implements Named {
    private final String name
    abstract Property<String> getZero()
   @javax.inject.Inject
   ExtensionSpec(String name) {
        this.name = name
    }
    @Override
    String getName() {
        return name
    }
}
abstract class ExtensionSpecOne extends ExtensionSpec {
    abstract Property<String> getOne()
    @javax.inject.Inject
    ExtensionSpecOne(String name) {
        super(name)
    }
}
abstract class ExtensionSpecTwo extends ExtensionSpec {
    @javax.inject.Inject
    ExtensionSpecTwo(String name) {
        super(name)
    }
}

class MyExt {
    private final ExtensiblePolymorphicDomainObjectContainer<ExtensionSpec> extensionSpecContainer

    @javax.inject.Inject
    MyExt(ObjectFactory objectFactory) {
        extensionSpecContainer = objectFactory.polymorphicDomainObjectContainer( ExtensionSpec )
//        extensionSpecContainer.registerFactory( ExtensionSpec, { objectFactory.newInstance(ExtensionSpec, it) } )
//        extensionSpecContainer.registerFactory( ExtensionSpecOne, { objectFactory.newInstance(ExtensionSpecOne, it) } )
//        extensionSpecContainer.registerFactory( ExtensionSpecTwo, { objectFactory.newInstance(ExtensionSpecTwo, it) } )
        extensionSpecContainer.registerFactory(
                ExtensionSpec,
                new NamedDomainObjectFactory() {
                    @Override
                    ExtensionSpec create(String name) {
                        return objectFactory.newInstance(ExtensionSpec, name)
                    }
                }
        )
        extensionSpecContainer.registerFactory(
                ExtensionSpecOne,
                new NamedDomainObjectFactory() {
                    @Override
                    ExtensionSpecOne create(String name) {
                        return objectFactory.newInstance(ExtensionSpecOne, name)
                    }
                }
        )
        extensionSpecContainer.registerFactory(
                ExtensionSpecTwo,
                new NamedDomainObjectFactory() {
                    @Override
                    ExtensionSpecTwo create(String name) {
                        return objectFactory.newInstance(ExtensionSpecTwo, name)
                    }
                }
        )
    }

    void extensionSpecs(Action<PolymorphicDomainObjectContainer<ExtensionSpec>> action) {
        action.execute( extensionSpecContainer );
    }

    PolymorphicDomainObjectContainer<ExtensionSpec> getExtensionSpecs() {
        return extensionSpecContainer
    }
}

extensions.create( 'myExt', MyExt )

myExt {
    extensionSpecs {
        a(ExtensionSpecOne) {
            zero = 'hello'
            one = 'world'
        }
        b(ExtensionSpecTwo) {
            zero = 'or not'
        }
        c(ExtensionSpecOne) {
            zero = 'bye'
            one = 'for now'
        }
        d {
            zero = 'so long'
        }
    }

}

task qwe {
    doLast {
        myExt.extensionSpecs.withType( ExtensionSpec ) {
            println "${it.name}: ${it.zero.get()}"
        }
        myExt.extensionSpecs.withType( ExtensionSpecOne ) {
            println "[One] ${it.name}: ${it.zero.get()} ${it.one.get()}"
        }
        myExt.extensionSpecs.withType( ExtensionSpecTwo ) {
            println "[Two] ${it.name} ${it.zero.get()}"
        }
    }
}
